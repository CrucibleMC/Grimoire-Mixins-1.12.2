buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

allprojects {
    apply plugin: 'java'
}

subprojects {
    apply plugin: 'idea'
    apply plugin: 'net.minecraftforge.gradle.forge'
    apply plugin: 'base'
    apply plugin: 'maven'

    minecraft {
        version = "1.12.2-14.23.4.2705"
        mappings = "stable_39"
        makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    }

    group = 'io.github.crucible'
    version = '1.0.1a'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'UTF-8'

    repositories {
        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/maven/'
        }
        mavenCentral()
    }

    dependencies {
        compile "org.spongepowered:mixin:0.8.1-SNAPSHOT"
        compileOnly 'org.jetbrains:annotations:16.0.2'
        compileOnly fileTree(dir: 'libs', include: ['*.jar'])
    }

    ext {
        mixinSrg = new File(project.buildDir, 'mixins/mixin.grimoire.srg')
        mixinRefMapName = name.toLowerCase() + ".refmap.json"; //Sub-Project Name
        mixinRefMap = new File(project.buildDir, "mixins/" + mixinRefMapName)
    }

    jar {
        from project.mixinRefMap
    }

    compileJava {
        options.compilerArgs += [
                '-Xlint:-processing',
                "-AoutSrgFile=${project.mixinSrg.canonicalPath}",
                "-AoutRefMapFile=${project.mixinRefMap.canonicalPath}",
                "-AreobfSrgFile=${project.file('build/srgs/mcp-srg.srg').canonicalPath}"
        ]
    }

    processResources{
        eachFile{
            filter ReplaceTokens, tokens:[MIXIN_REFMAP_NAME: mixinRefMapName]
        }
    }

    //Intelij IDEAL has a "BUG" that hides
    // theses functions bellow from the
    // subprojects gradle. Altering them
    // make them reapear

    tasks.build.doFirst {
        println "Grimoire BuildProject"
    }

    tasks.clean.doFirst {
        println "Grimoire CleanProject!"
    }
}

